; WYZ player
;
; Hardware output routine for CPC
;
;

	SECTION	code_psg

	PUBLIC	asm_wyz_hardware_out

	EXTERN	asm_wyz_PSG_REG
	EXTERN	asm_wyz_PSG_REG_SEC
	EXTERN	asm_wyz_ENVOLVENTE_BACK


asm_wyz_hardware_out:
 LD A,(asm_wyz_PSG_REG+13)
 AND A ;ES CERO?
 JR Z,NO_BACKUP_ENVOLVENTE
 LD (asm_wyz_ENVOLVENTE_BACK),A ;08.13 / GUARDA LA ENVOLVENTE EN EL BACKUP
 XOR A

 NO_BACKUP_ENVOLVENTE:
;VUELCA BUFFER DE SONIDO AL PSG
 LD HL,asm_wyz_PSG_REG_SEC
LOUT:
 CALL WRITEPSGHL
 INC A
 CP 13
 JR NZ,LOUT
 LD A,(HL)
 AND A
 RET Z
 LD A,13
 CALL WRITEPSGHL
 XOR A
 LD (asm_wyz_PSG_REG+13),A
 LD (asm_wyz_PSG_REG_SEC+13),A
 RET

;; A = REGISTER
;; (HL) = VALUE
WRITEPSGHL:
 LD B,$F4
 OUT (C),A
 LD BC,$F6C0
 OUT (C),C
 DEFB $ED	;out (c),0
 DEFB $71
 LD B,$F5
 OUTI
 LD BC,$F680
 OUT (C),C
 DEFB $ED
 DEFB $71
 RET
